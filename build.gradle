plugins {
    id 'java-library'
    id 'eclipse'
    id 'idea'
    id 'net.neoforged.gradle.userdev' version '7.0.165'
    id 'com.diffplug.spotless' version '6.25.0'
}

version = "${minecraft_version}-${mod_version}"
group = 'platinpython.bonkbat'
base.archivesName = 'Bonk Bat'

java.toolchain.languageVersion = JavaLanguageVersion.of(21)

println "Java: ${System.getProperty 'java.version'}, JVM: ${System.getProperty 'java.vm.version'} (${System.getProperty 'java.vendor'}), Arch: ${System.getProperty 'os.arch'}"

runs {
    configureEach {
        systemProperty 'forge.logging.markers', 'REGISTRIES'

        systemProperty 'forge.logging.console.level', 'debug'

        systemProperty 'mixin.debug.verbose', 'true'
        systemProperty 'mixin.debug.export', 'true'

        modSource project.sourceSets.main
    }

    client {
        systemProperty 'forge.enabledGameTestNamespaces', 'bonkbat'
    }

    server {
        systemProperty 'forge.enabledGameTestNamespaces', 'bonkbat'
        argument '--nogui'
    }

    data {
        arguments.addAll '--mod', 'bonkbat', '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

dependencies {
    implementation 'net.neoforged:neoforge:21.3.2-beta'

    compileOnly 'org.jspecify:jspecify:1.0.0'
}

jar {
    manifest {
        attributes([
                'Specification-Title'     : "bonkbat",
                'Specification-Vendor'    : "PlatinPython",
                'Specification-Version'   : '1',
                'Implementation-Title'    : project.name,
                'Implementation-Version'  : project.jar.archiveVersion,
                'Implementation-Vendor'   : "PlatinPython",
                'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

apply from: rootProject.file('codeformat/spotless-rules.gradle')
